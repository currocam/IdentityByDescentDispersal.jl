initialize() {
	// Set up a 2 dimensional torus
	initializeSLiMOptions(dimensionality="xy");
	initializeTreeSeq();
	initializeMutationType("m1", 0.5, "f", 0.0);
	initializeGenomicElementType("g1", m1, 1.0);
	// Simulate a 1 Morgan chromosome
	initializeGenomicElement(g1, 0, 1e8 - 1);
	initializeRecombinationRate(1e-8);
	initializeMutationRate(0.0);
	// Define constants:
	defineConstantIfInteractive("NE", 50); // Number of individuals
	defineConstantIfInteractive("SM", 0.01); // Mate choice
	defineConstantIfInteractive("SD", 0.1); // Dispersal
	// Set up spatial interaction type
	// We use a Gaussian kernel to choose a mate
	initializeInteractionType(1, "xy", reciprocal=T, maxDistance=SM*3);
	i1.setInteractionFunction("n", 1.0, SM);
	// We define a second kernel to keep track of the local population density
	// experienced by the population
	// We have to account for differences between the dispersal gestating mate - offspring
	// and the non-gestating mate - offspring
	defineConstant("SIGMA", sqrt(SD^2 + SM^2 / 2));
	defineConstant("RUNTIME", 20000);
	defineGlobal("LOCAL_DENSITY", float(RUNTIME));
	defineConstantIfInteractive("OUTPATH", "out.trees");
}

mateChoice() {
	// Spatial mate choice based on interaction strength
	return i1.strength(individual);
}

1 early() {
	sim.addSubpop("p1", NE);
	p1.individuals.setSpatialPosition(p1.pointUniform(NE));
	i1.evaluate(p1);
}


1: late() {
	p1.deviatePositions(NULL, "reprising", INF, "n", SD);
	inds = p1.individuals;
	bounds = p1.spatialBounds;
	// make a density map: 0 is empty, 1 is maximum density
	density = summarizeIndividuals(inds, c(10, 10), bounds,
		operation="individuals.size();", empty=0.0, perUnitArea=T);
	p1.defineSpatialMap("density", "xy", density, F,
		range(density), c("black", "orange", "red"));
	LOCAL_DENSITY[sim.cycle-1] = 	mean(density);
}

2: first() {
	i1.evaluate(p1);

}

RUNTIME late() {
	sim.treeSeqOutput(
		OUTPATH,
		metadata=Dictionary("SIGMA", SIGMA, "D", NE, "D_empirical", mean(LOCAL_DENSITY))
		);
}

function (void)defineConstantIfInteractive(string$ symbol, * value){
	if (exists("slimgui")) defineConstant(symbol, value);
}
